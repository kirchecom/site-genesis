prototype(KircheCom.SiteGenesis:Document.Fragment.Menu.Main) < prototype(Neos.Fusion:Component) {
    root = ${site}
    menuItems = Neos.Neos:MenuItems
    toggleNavigationLabel = ${Translation.translate('KircheCom.SiteGenesis:NodeTypes.Document.Fragment.Menu.Main:toggleNavigation')}
    logo = Neos.Fusion:Template {
        templatePath = 'resource://KircheCom.SiteGenesis/Private/Templates/logo.svg'
        width = 36
        height = 36
    }

    renderer = afx`
        <nav class="kirchecom-sitegenesis-document-fragment-menu-main navbar navbar-expand-md navbar-light bg-white border shadow-sm mt-2 mt-lg-3 p-0">
            <Neos.Neos:NodeLink node={props.root} attributes.class="navbar-brand bg-dark mr-0 px-3 py-2">
                {props.logo}
            </Neos.Neos:NodeLink>
            <button class="navbar-toggler mr-2" type="button" data-toggle="collapse" data-target="#theNavbar"
                aria-controls="theNavbar" aria-expanded="false" aria-label={props.toggleNavigationLabel}>
                <span class="navbar-toggler-icon"></span>
            </button>
            <div id="theNavbar" class="collapse navbar-collapse">
                <ul class="navbar-nav mr-auto">
                    <Neos.Fusion:Loop items={props.menuItems}>
                        <li class={item.state == 'current' ? 'active nav-item' : 'nav-item'}>
                            <Neos.Neos:NodeLink node={item.node}>{item.label}</Neos.Neos:NodeLink>
                        </li>
                    </Neos.Fusion:Loop>
                </ul>
            </div>
        </nav>
    `

    @cache {
        mode = 'cached'
        entryIdentifier {
            documentNode = ${documentNode}
        }
        entryTags {
            1 = ${Neos.Caching.nodeTypeTag('Neos.Neos:Document', documentNode)}
        }
    }
}
