prototype(KircheCom.SiteGenesis:Component.Atom.Blockquote) < prototype(Neos.Fusion:Component) {
    text = null
    reference = null
    link = null

    renderer = afx`
        <blockquote class="kirchecom-blockquote blockquote">
            <div>{props.text}</div>
            <footer @if.has={props.reference} class="blockquote-footer text-muted">
                <cite @if.has={props.link}>
                    <a href={props.link} target="_blank" rel="noopener">
                        {props.reference}
                    </a>
                </cite>
                <cite @if.has={!props.link}>
                    {props.reference}
                </cite>
            </footer>
        </blockquote>
    `

    @styleguide {
        title = 'Blockquote'
        description = '
            Basic component for quotes.
            Can be rendered either with or without the author of the quote (props.reference).
            Optionally an url can be provided (props.link) which will render the author as a link.
            The link always opens in a new browser tab.
        '
        props {
            text = 'Spread love everywhere you go. Let no one ever come to you without leaving happier.'
            reference = 'Mother Teresa'
            link = ''
        }
        propSets {
            'Without Author' {
                text = 'Live, Laugh, Love'
                reference = ''
                link = ''
            }
            'With Link' {
                text = 'I cannot and will not recant anything, since it is neither safe nor right to go against conscience. May God help me. Amen.'
                reference = 'Martin Luther'
                link = 'https://en.wikipedia.org/wiki/Martin_Luther#Diet_of_Worms'
            }
        }
    }
}
